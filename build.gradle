apply plugin: 'java-library'
apply plugin: 'maven-publish'

group = 'com.velocitypowered'
version = project.hasProperty('buildNumber') ? "${project.majorMinor}.${project.buildNumber}" : "${project.majorMinor}.0-SNAPSHOT"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://libraries.minecraft.net"
        }
    }
}

repositories {
    maven {
        url "https://libraries.minecraft.net"
    }
    mavenCentral()
}

java {
    withSourcesJar();
}

dependencies {
    testImplementation 'com.google.guava:guava:26.0-jre'
    testImplementation 'junit:junit-dep:4.11'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'com.google.guava:guava-testlib:26.0-jre'
    testImplementation 'org.openjdk.jmh:jmh-core:1.21'
    annotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.21'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

test {
    testLogging {
        events "failed", "skipped"
        showStandardStreams = true
        showExceptions true
    }
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task report {
    doLast {
        println "##vso[build.updatebuildnumber]${project.version}"
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username System.getenv("NEXUS_USERNAME")
                password System.getenv("NEXUS_PASSWORD")
            }

            name = 'velocity-nexus'
            def base = 'https://repo.papermc.io/repository/maven'
            def releasesRepoUrl = "$base-releases/"
            def snapshotsRepoUrl = "$base-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}